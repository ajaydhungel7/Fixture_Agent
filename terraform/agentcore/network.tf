data "aws_availability_zones" "available" {
  state = "available"
}

resource "aws_vpc" "main" {
  count                = var.agentcore_use_vpc ? 1 : 0
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.project_name}-agentcore-vpc"
  }
}

resource "aws_internet_gateway" "main" {
  count  = var.agentcore_use_vpc ? 1 : 0
  vpc_id = aws_vpc.main[0].id

  tags = {
    Name = "${var.project_name}-agentcore-igw"
  }
}

resource "aws_subnet" "public" {
  count                   = var.agentcore_use_vpc ? 2 : 0
  vpc_id                  = aws_vpc.main[0].id
  cidr_block              = var.public_subnet_cidrs[count.index]
  availability_zone       = data.aws_availability_zones.available.names[count.index]
  map_public_ip_on_launch = true

  tags = {
    Name = "${var.project_name}-agentcore-public-${count.index + 1}"
  }
}

resource "aws_route_table" "public" {
  count  = var.agentcore_use_vpc ? 1 : 0
  vpc_id = aws_vpc.main[0].id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main[0].id
  }

  tags = {
    Name = "${var.project_name}-agentcore-public-rt"
  }
}

resource "aws_route_table_association" "public" {
  count          = var.agentcore_use_vpc ? 2 : 0
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public[0].id
}

resource "aws_security_group" "agentcore" {
  count  = var.agentcore_use_vpc ? 1 : 0
  name   = "${var.project_name}-agentcore-sg"
  vpc_id = aws_vpc.main[0].id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
